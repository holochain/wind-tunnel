import { input, exit } from "std/env"
import { lines } from "std/text"

let cmd = input("Enter the command to run: ")

let x = $cat "./targets.yaml" | grep "ws://"$ failed {
    echo "[{status}] Failed to read file"
    exit(1)
}

let attempted = 0
let succeeded = 0

loop index, line in lines(x) {
    let ip = $echo {line} | cut -b 8- | rev | cut -b 6- | rev$ failed {
        echo "[{status}] failed to extract ip from {line}"
        continue
    }
    echo "Running on target {index} at {ip}"
    attempted = attempted + 1
    let result = $ssh "-oPreferredAuthentications publickey" "root@{ip}" {cmd}$ failed {
        echo "[{status}] failed to run command on {ip}"
        continue
    }
    succeeded = succeeded + 1
    echo result
}

echo "Ran command on successfully on {succeeded}/{attempted} targets"
